buildscript {
    ext.kotlinVersion = "1.2.50"

    repositories {
        google()
        maven { url "https://maven.fabric.io/public" }
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:3.1.3"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "io.fabric.tools:gradle:1.25.1"
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = "4.5"
}

apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "io.fabric"

Properties properties = new Properties()
properties.load(rootProject.file("local.properties").newInputStream())

def enableCrashlytics = properties.getProperty("fabric.key") != null

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    defaultConfig {
        applicationId "ru.dyatel.tsuschedule"
        minSdkVersion 17
        targetSdkVersion 27
        versionCode 17
        versionName "1.2.2"

        manifestPlaceholders = ["fabricKey": enableCrashlytics ? properties.getProperty("fabric.key") : ""]
    }

    if (properties.getProperty("signing.storeFile") != null) {
        signingConfigs {
            release {
                storeFile file(properties.getProperty("signing.storeFile"))
                storePassword properties.getProperty("signing.storePassword")
                keyAlias properties.getProperty("signing.keyAlias")
                keyPassword properties.getProperty("signing.keyPassword")
            }
        }

        buildTypes.release.signingConfig signingConfigs.release
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"

            buildConfigField "boolean", "ENABLE_CRASHLYTICS", "false"
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"

            buildConfigField "boolean", "ENABLE_CRASHLYTICS", "$enableCrashlytics"
        }
    }

    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
        test.java.srcDirs += "src/test/kotlin"
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${rootProject.name}-${variant.versionName}-${variant.name}.apk"
        }
    }
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

repositories {
    jcenter()
    maven { url "https://maven.google.com" }
    maven { url "https://maven.fabric.io/public" }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:0.23.3"

    def androidSupportVersion = "27.1.1"
    implementation "com.android.support:recyclerview-v7:$androidSupportVersion"
    implementation "com.android.support:cardview-v7:$androidSupportVersion"
    implementation "com.android.support:appcompat-v7:$androidSupportVersion"
    implementation "com.android.support:support-v13:$androidSupportVersion"
    implementation "com.android.support:preference-v14:$androidSupportVersion"
    implementation "com.android.support:design:$androidSupportVersion"

    def magellanVersion = "1.1.0"
    implementation "com.wealthfront:magellan:$magellanVersion"
    implementation "com.wealthfront:magellan-support:$magellanVersion"

    def fastAdapterVersion = "3.2.7"
    implementation "com.mikepenz:fastadapter:$fastAdapterVersion@aar"
    implementation "com.mikepenz:fastadapter-commons:$fastAdapterVersion@aar"
    implementation "com.mikepenz:fastadapter-extensions:$fastAdapterVersion@aar"

    implementation "com.mikepenz:materialize:1.1.2@aar"
    implementation("com.mikepenz:materialdrawer:6.0.8@aar") { transitive = true }
    implementation "com.mikepenz:iconics-core:3.0.4@aar"
    implementation "com.mikepenz:community-material-typeface:2.0.46.1@aar"

    def ankoVersion = "0.10.5"
    implementation "org.jetbrains.anko:anko-commons:$ankoVersion"
    implementation "org.jetbrains.anko:anko-sdk15:$ankoVersion"
    implementation "org.jetbrains.anko:anko-support-v4:$ankoVersion"
    implementation "org.jetbrains.anko:anko-appcompat-v7:$ankoVersion"
    implementation "org.jetbrains.anko:anko-recyclerview-v7:$ankoVersion"
    implementation "org.jetbrains.anko:anko-cardview-v7:$ankoVersion"
    implementation "org.jetbrains.anko:anko-design:$ankoVersion"
    implementation "org.jetbrains.anko:anko-sqlite:$ankoVersion"

    implementation "org.jsoup:jsoup:1.11.3"

    implementation "com.darwinsys:hirondelle-date4j:1.5.1"

    implementation("com.crashlytics.sdk.android:crashlytics:2.9.4@aar") { transitive = true }

    debugImplementation "com.amitshekhar.android:debug-db:1.0.4"

    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlinVersion"
}
