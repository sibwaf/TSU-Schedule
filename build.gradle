buildscript {
    ext.kotlinVersion = "1.1.2-2"

    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:2.2.3"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

apply plugin: "com.android.application"
apply plugin: "kotlin-android"

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    Properties properties = new Properties()
    properties.load(file("local.properties").newDataInputStream())

    defaultConfig {
        applicationId "ru.dyatel.tsuschedule"
        minSdkVersion 17
        targetSdkVersion 25
        versionCode 5
        versionName "0.7.1"

        buildConfigField "String", "ACRA_BACKEND", properties.getProperty("ACRA_BACKEND")
    }

    signingConfigs {
        release {
            storeFile file(properties.get("signing.storeFile"))
            storePassword properties.get("signing.storePassword")
            keyAlias properties.get("signing.keyAlias")
            keyPassword properties.get("signing.keyPassword")
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"

            signingConfig signingConfigs.release
        }
    }

    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
        test.java.srcDirs += "src/test/kotlin"
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(output.outputFile.parent,
                    "${rootProject.name}-${variant.versionName}-${output.baseName}.apk")
        }
    }
}

repositories {
    jcenter()
}

ext {
    androidSupportVersion = "25.3.1"
    drawerVersion = "5.9.0"
    ankoVersion = "0.9.1a"
    jsoupVersion = "1.10.2"
    date4jVersion = "1.5.1"
    acraVersion = "4.9.2"

    junitVersion = "4.12"
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    compile "com.android.support:recyclerview-v7:$androidSupportVersion"
    compile "com.android.support:cardview-v7:$androidSupportVersion"
    compile "com.android.support:appcompat-v7:$androidSupportVersion"
    compile "com.android.support:support-v13:$androidSupportVersion"

    compile "com.android.support:design:$androidSupportVersion"

    compile "com.mikepenz:materialdrawer:$drawerVersion"

    compile "org.jetbrains.anko:anko-common:$ankoVersion"
    compile "org.jetbrains.anko:anko-sqlite:$ankoVersion"

    compile "org.jsoup:jsoup:$jsoupVersion"

    compile "com.darwinsys:hirondelle-date4j:$date4jVersion"

    compile "ch.acra:acra:$acraVersion"

    testCompile "junit:junit:$junitVersion"
}
