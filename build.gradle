buildscript {
    ext.kotlinVersion = "1.2.10"

    repositories {
        google()
        jcenter()
        maven { url "https://maven.fabric.io/public" }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:2.3.3"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "io.fabric.tools:gradle:1.25.1"
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = "4.4.1"
}

apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "io.fabric"

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    Properties properties = new Properties()
    properties.load(rootProject.file("local.properties").newDataInputStream())

    defaultConfig {
        applicationId "ru.dyatel.tsuschedule"
        minSdkVersion 17
        targetSdkVersion 27
        versionCode 11
        versionName "1.0.1"

        manifestPlaceholders = [fabricKey: properties.getProperty("fabric.key")]
    }

    signingConfigs {
        release {
            storeFile file(properties.get("signing.storeFile"))
            storePassword properties.get("signing.storePassword")
            keyAlias properties.get("signing.keyAlias")
            keyPassword properties.get("signing.keyPassword")
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"

            signingConfig signingConfigs.release
        }
    }

    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
        test.java.srcDirs += "src/test/kotlin"
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(output.outputFile.parent,
                    "${rootProject.name}-${variant.versionName}-${output.baseName}.apk")
        }
    }
}

repositories {
    jcenter()
    maven { url "https://maven.google.com" }
    maven { url "https://maven.fabric.io/public" }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    def androidSupportVersion = "27.0.2"
    compile "com.android.support:recyclerview-v7:$androidSupportVersion"
    compile "com.android.support:cardview-v7:$androidSupportVersion"
    compile "com.android.support:appcompat-v7:$androidSupportVersion"
    compile "com.android.support:support-v13:$androidSupportVersion"
    compile "com.android.support:design:$androidSupportVersion"

    def magellanVersion = "1.0.0"
    compile "com.wealthfront:magellan:$magellanVersion"
    compile "com.wealthfront:magellan-support:$magellanVersion"

    compile("com.mikepenz:materialdrawer:6.0.2@aar") { transitive = true }
    compile "com.mikepenz:iconics-core:3.0.0@aar"
    compile "com.mikepenz:community-material-typeface:2.0.46.1@aar"

    def ankoVersion = "0.10.4"
    compile "org.jetbrains.anko:anko-commons:$ankoVersion"
    compile "org.jetbrains.anko:anko-sdk15:$ankoVersion"
    compile "org.jetbrains.anko:anko-recyclerview-v7:$ankoVersion"
    compile "org.jetbrains.anko:anko-design:$ankoVersion"
    compile "org.jetbrains.anko:anko-sqlite:$ankoVersion"

    compile "org.jsoup:jsoup:1.11.2"

    compile "com.darwinsys:hirondelle-date4j:1.5.1"

    compile("com.crashlytics.sdk.android:crashlytics:2.8.0@aar") { transitive = true }

    debugCompile "com.amitshekhar.android:debug-db:1.0.1"

    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlinVersion"
}
