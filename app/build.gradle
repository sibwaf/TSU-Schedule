apply plugin: "com.android.application"
apply plugin: "kotlin-android"

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    Properties properties = new Properties()
    properties.load(project.rootProject.file("local.properties").newDataInputStream())

    defaultConfig {
        applicationId "ru.dyatel.tsuschedule"
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 5
        versionName "0.7.1"

        buildConfigField "String", "ACRA_BACKEND", properties.getProperty("ACRA_BACKEND")
    }

    signingConfigs {
        release {
            storeFile file(properties.get("signing.storeFile"))
            storePassword properties.get("signing.storePassword")
            keyAlias properties.get("signing.keyAlias")
            keyPassword properties.get("signing.keyPassword")
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"

            signingConfig signingConfigs.release
        }
    }

    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(output.outputFile.parent,
                    "${rootProject.name}-${variant.versionName}-${output.baseName}.apk")
        }
    }
}

dependencies {
    compile project(":util")
    compile project(":parser")

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    compile "com.android.support:recyclerview-v7:$androidSupportVersion"
    compile "com.android.support:cardview-v7:$androidSupportVersion"
    compile "com.android.support:appcompat-v7:$androidSupportVersion"
    compile "com.android.support:preference-v7:$androidSupportVersion"

    compile "com.android.support:design:$androidSupportVersion"

    compile "org.jetbrains.anko:anko-common:$ankoVersion"
    compile "org.jetbrains.anko:anko-sqlite:$ankoVersion"

    compile "com.darwinsys:hirondelle-date4j:$date4jVersion"

    compile "ch.acra:acra:$acraVersion"
}
