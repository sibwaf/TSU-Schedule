apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "io.fabric"

android {
    compileSdkVersion 25
    buildToolsVersion "26.0.1"

    Properties properties = new Properties()
    properties.load(rootProject.file("local.properties").newDataInputStream())

    defaultConfig {
        applicationId "ru.dyatel.tsuschedule"
        minSdkVersion 17
        targetSdkVersion 25
        versionCode 5
        versionName "0.7.1"

        manifestPlaceholders = [ fabricKey: properties.getProperty("fabric.key") ]
    }

    signingConfigs {
        release {
            storeFile file(properties.get("signing.storeFile"))
            storePassword properties.get("signing.storePassword")
            keyAlias properties.get("signing.keyAlias")
            keyPassword properties.get("signing.keyPassword")
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"

            signingConfig signingConfigs.release
        }
    }

    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
        test.java.srcDirs += "src/test/kotlin"
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(output.outputFile.parent,
                    "${rootProject.name}-${variant.versionName}-${output.baseName}.apk")
        }
    }
}

repositories {
    jcenter()
    maven { url "https://maven.google.com" }
    maven { url "https://maven.fabric.io/public" }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    def androidSupportVersion = "25.4.0"
    compile "com.android.support:recyclerview-v7:$androidSupportVersion"
    compile "com.android.support:cardview-v7:$androidSupportVersion"
    compile "com.android.support:appcompat-v7:$androidSupportVersion"
    compile "com.android.support:support-v13:$androidSupportVersion"
    compile "com.android.support:design:$androidSupportVersion"

    compile("com.mikepenz:materialdrawer:5.9.4@aar") { transitive = true }
    compile "com.mikepenz:iconics-core:2.9.1@aar"
    compile "com.mikepenz:google-material-typeface:3.0.1.1.original@aar"

    def ankoVersion = "0.10.1"
    compile "org.jetbrains.anko:anko-commons:$ankoVersion"
    compile "org.jetbrains.anko:anko-sdk15:$ankoVersion"
    compile "org.jetbrains.anko:anko-sqlite:$ankoVersion"

    compile "org.jsoup:jsoup:1.10.3"

    compile "com.darwinsys:hirondelle-date4j:1.5.1"

    compile("com.crashlytics.sdk.android:crashlytics:2.6.8@aar") { transitive = true }

    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlinVersion"
}
