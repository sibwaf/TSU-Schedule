apply plugin: "com.android.application"
apply plugin: "kotlin-android"

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.1"

    Properties properties = new Properties()
    properties.load(rootProject.file("local.properties").newDataInputStream())

    defaultConfig {
        applicationId "ru.dyatel.tsuschedule"
        minSdkVersion 17
        targetSdkVersion 26
        versionCode 5
        versionName "0.7.1"

        buildConfigField "String", "ACRA_BACKEND", properties.getProperty("ACRA_BACKEND")
    }

    signingConfigs {
        release {
            storeFile file(properties.get("signing.storeFile"))
            storePassword properties.get("signing.storePassword")
            keyAlias properties.get("signing.keyAlias")
            keyPassword properties.get("signing.keyPassword")
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"

            signingConfig signingConfigs.release
        }
    }

    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
        test.java.srcDirs += "src/test/kotlin"
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(output.outputFile.parent,
                    "${rootProject.name}-${variant.versionName}-${output.baseName}.apk")
        }
    }
}

repositories {
    jcenter()
    maven { url "https://maven.google.com" }
}

ext {
    androidSupportVersion = "26.0.1"
    ankoVersion = "0.10.1"
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    compile "com.android.support:recyclerview-v7:$androidSupportVersion"
    compile "com.android.support:cardview-v7:$androidSupportVersion"
    compile "com.android.support:appcompat-v7:$androidSupportVersion"
    compile "com.android.support:support-v13:$androidSupportVersion"
    compile "com.android.support:design:$androidSupportVersion"

    compile("com.mikepenz:materialdrawer:5.9.5@aar") { transitive = true }
    compile "com.mikepenz:iconics-core:2.9.1@aar"
    compile "com.mikepenz:google-material-typeface:3.0.1.1.original@aar"

    compile "org.jetbrains.anko:anko-common:$ankoVersion"
    compile "org.jetbrains.anko:anko-sqlite:$ankoVersion"

    compile "org.jsoup:jsoup:1.10.3"

    compile "com.darwinsys:hirondelle-date4j:1.5.1"

    compile "ch.acra:acra:4.9.2"

    testCompile "junit:junit:4.12"
    testCompile "org.hamcrest:hamcrest-library:1.3"
}
