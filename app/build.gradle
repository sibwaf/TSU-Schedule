apply plugin: "com.android.application"
apply plugin: "kotlin-android"

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.2"

    Properties properties = new Properties()
    properties.load(project.rootProject.file("local.properties").newDataInputStream())

    defaultConfig {
        applicationId "ru.dyatel.tsuschedule"
        minSdkVersion 14
        targetSdkVersion 24
        versionCode 4
        versionName "0.6.1"

        buildConfigField "String", "ACRA_BACKEND", properties.getProperty("ACRA_BACKEND")

        buildConfigField "boolean", "DISABLE_ACRA", "false"
    }

    signingConfigs {
        release {
            storeFile file(properties.get("signing.storeFile"))
            storePassword properties.get("signing.storePassword")
            keyAlias properties.get("signing.keyAlias")
            keyPassword properties.get("signing.keyPassword")
        }
    }

    buildTypes {
        debug {
            buildConfigField "boolean", "DISABLE_ACRA", "true"
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"

            signingConfig signingConfigs.release
        }
    }

    sourceSets {
        main.java.srcDirs += kotlinSourceDir
    }
}

def supportVersion = "24.2.0"

dependencies {
    compile project(":util")
    compile project(":parser")

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    compile "com.android.support:recyclerview-v7:$supportVersion"
    compile "com.android.support:cardview-v7:$supportVersion"
    compile "com.android.support:appcompat-v7:$supportVersion"
    compile "com.android.support:preference-v7:$supportVersion"

    compile "com.android.support:design:$supportVersion"

    compile "ch.acra:acra:4.9.0"
}
